buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'gradle.plugin.com.onesignal:onesignal-gradle-plugin:[0.11.0, 0.99.99]'
    }
}

apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'
apply plugin: 'com.android.application'

android {

    Properties properties = new Properties()
    properties.load(project.rootProject.file('secret.properties').newDataInputStream())
    Properties versionProps = new Properties()
    def versionPropsFile = project.rootProject.file('version.properties')
    versionProps.load(new FileInputStream(versionPropsFile))

    def major = 1
    def minor = 1
    def patch = 0
    def code = versionProps['VERSION_CODE'].toInteger() + 1
    versionProps['VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    signingConfigs {
        config {
            storeFile file(properties.getProperty('storeFile'))
            keyAlias properties.getProperty('keyAlias')
            storePassword properties.getProperty('storePassword')
            keyPassword properties.getProperty('keyPassword')
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "io.github.nkrusch.spacelaunchone"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode code
        versionName major + "." + minor + "." + patch + "." + code
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'int', "UpdateInterval", Min_Update_Interval
        buildConfigField 'int', "MissionTextClipLength", MissionText_ClipLength
        buildConfigField 'String', "CloudinaryClound", properties.getProperty('Cloudinary_Cloud')
        resValue("string", "google_maps_key", properties.getProperty('Google_Maps_Key'))
        manifestPlaceholders = [
                onesignal_app_id               : properties.getProperty('onesignal'),
                onesignal_google_project_number: 'REMOTE'
        ]
    }
    buildTypes {
        release {
            //  minifyEnabled true
            //  proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // support libraries
    implementation "androidx.legacy:legacy-support-v4:$androixVersion"
    implementation "androidx.legacy:legacy-support-v13:$androixVersion"
    implementation "androidx.vectordrawable:vectordrawable:$vectorVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.recyclerview:recyclerview:$androixVersion"
    implementation "androidx.cardview:cardview:$androixVersion"
    implementation "androidx.legacy:legacy-preference-v14:$androixVersion"
    implementation "com.google.android.material:material:$androixVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    // viewModels
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycleVersion"

    // 3rd party libs
    implementation "com.cloudinary:cloudinary-android:$cloudinaryVersion"
    implementation "com.squareup.picasso:picasso:$picassoVersion"
    implementation "com.google.android.gms:play-services-maps:$googleMapsVersion"
    implementation "com.google.android.gms:play-services-places:$googlePlacesVersion"
    implementation "com.onesignal:OneSignal:$oneSignalVersion"

    // project modules
    implementation project(':datasrc')
    implementation project(':models')

    // testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PUBLIC
    options.addStringOption("encoding", "UTF-8")
    options.addStringOption("charset", "UTF-8")
    destinationDir = file("../../javadoc/")
    failOnError false
}